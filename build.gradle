import java.text.SimpleDateFormat

version = '0.1.0-kotlin.M11'


buildscript {
    repositories { mavenCentral() }
    dependencies {
        classpath 'org.jetbrains.kotlin:kotlin-gradle-plugin:0.11.91'
    }
}

repositories { mavenCentral() }

apply plugin: 'kotlin'
apply plugin: 'maven'
apply plugin: 'signing'

dependencies {
    compile 'org.jetbrains.kotlin:kotlin-stdlib:0.11.91'
    testCompile 'junit:junit:4.12'
}



gradle.taskGraph.whenReady {taskGraph ->
    def circleBuildNr = System.getenv('CIRCLE_BUILD_NUM')
    if (circleBuildNr != null && !circleBuildNr.isEmpty()) {
        version = version + '+' + circleBuildNr
    } else {
        version = version + '+' + buildTime()
    }
}

def buildTime() {
    def df = new SimpleDateFormat("yyyy-MM-dd'T'HH:mm'Z'")
    df.setTimeZone(TimeZone.getTimeZone("UTC"))
    return df.format(new Date())
}

task libs() {
    dependsOn jar
}

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives javadocJar, sourcesJar
}

signing {
    sign configurations.archives
}

task wrapper(type: Wrapper) {
    gradleVersion = '2.3'
}

defaultTasks 'clean', 'libs'


